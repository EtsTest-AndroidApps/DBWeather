apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies { classpath 'io.fabric.tools:gradle:1.24.4' }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.dbeginc.dbweather"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        multiDexEnabled = true
        versionCode 4
        versionName "1.3"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true

        buildTypes.each {
            it.buildConfigField('String', 'YOUTUBE_API_KEY', YoutubeApiKey)
        }
    }

    lintOptions {
        //lint rules
        apply from: "${rootDir}/config/lint.gradle", to: it
        //locations for the rules and output
        lintConfig file("${rootDir}/config/lint/lint-config.xml")
        htmlOutput file("${buildDir}/reports/lint/lint.html")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            minifyEnabled false
            shrinkResources false
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //Show all the result of unitTest
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    configurations.all() {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:+'
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/INDEX.LIST'
    }

    dataBinding { enabled = true }

    testOptions { unitTests.returnDefaultValues = true }

    dexOptions { preDexLibraries = false }

    sourceSets {
        test.java { srcDirs += 'src/test/kotlin' }

        androidTest.java { srcDirs += 'src/androidTest/kotlin' }

        main {
            java { srcDirs += 'src/main/kotlin' }
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(":dbweatherdata")
    implementation project(":dbweatherdomain")
    implementation project(":dbweathernews")
    implementation project(":dbweatherweather")

    androidTestImplementation(androidSupportLibs.espresso) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    //GOOGLE
    implementation androidSupportLibs.constain_layout
    implementation androidSupportLibs.multidex
    implementation androidSupportLibs.appcompat_v7
    implementation androidSupportLibs.design
    implementation androidSupportLibs.recyclerview
    implementation androidSupportLibs.support_v13
    implementation androidSupportLibs.support_core_utils
    implementation androidSupportLibs.support_v4
    implementation androidSupportLibs.customtabs
    implementation androidSupportLibs.cardview_v7
    implementation apache.commons_lang3
    // Android Arch Lifecycle
    implementation architectureComponentLibs.lifecycle_extensions

    //Dagger
    implementation daggerLibs.dagger
    implementation daggerLibs.dagger_android
    implementation daggerLibs.dagger_android_support

    compileOnly rxLibs.rxJava
    compileOnly kotlinLibs.kotlin_test_junit

    //SQUARE
    implementation otherLibs.threetenabp

    //OTHER
    implementation "org.jetbrains.anko:anko-coroutines:0.10.3"

    implementation('com.crashlytics.sdk.android:crashlytics:2.7.0@aar') {
        transitive = true
    }
    implementation files('libs/YouTubeAndroidPlayerApi.jar')

    // Google libs
    implementation googleLibs.firebase_ads
    implementation googleLibs.play_services_location
    // Glide libs
    implementation glideLibs.glide
    implementation glideLibs.annotations
    implementation glideLibs.okhttp3_integration
    implementation  (glideLibs.recyclerview_integration) {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }

    implementation otherLibs.evernote_android_job
    implementation otherLibs.lottie
    implementation otherLibs.bottom_bar
    implementation otherLibs.fab
    implementation otherLibs.viewAnimator
    testImplementation otherLibs.junit
    testImplementation otherLibs.mockito

    kapt daggerLibs.dagger
    kapt daggerLibs.dagger_processor
    kapt androidSupportLibs.databinding
    kapt architectureComponentLibs.lifecycle_compiler
    kapt glideLibs.compiler
}

configurations {
    implementation.exclude group: "org.apache.httpcomponents", module: "httpclient"
    implementation.exclude group: "xpp3", module: "xpp3"
}

apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'

////code quality tools
apply from: "${rootDir}/config/codequality-findbugs.gradle"
apply from: "${rootDir}/config/codequality-pmd.gradle"
apply from: "${rootDir}/config/codequality-checkstyle.gradle"

////code coverage
apply from: "${rootDir}/config/codecoverage.gradle"
